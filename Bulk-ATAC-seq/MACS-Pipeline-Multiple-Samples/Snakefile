"""
Bulk ATAC-seq analysis pipeline.
"""

configfile: "config.yaml"

import yaml
import sys
import os

ALL_SAMPLES1 = config["samples1"]
ALL_SAMPLES2 = config["samples2"]

PEAKS1   = expand("Result/Analysis/{sample}_peaks.narrowPeak", sample=ALL_SAMPLES1)
PEAKS2   = expand("Result/Analysis/{sample}_peaks.narrowPeak", sample=ALL_SAMPLES2)
SEQSTAT1 = expand("Result/QC/{sample}.stat.txt", sample=ALL_SAMPLES1)
SEQSTAT2 = expand("Result/QC/{sample}.stat.txt", sample=ALL_SAMPLES2)
PEAKSTAT1= expand("Result/QC/{sample}.peakstat.txt", sample=ALL_SAMPLES1)
PEAKSTAT2= expand("Result/QC/{sample}.peakstat.txt", sample=ALL_SAMPLES2)

TARGET = []
TARGET.extend(PEAKS1 + PEAKS2 + SEQSTAT1 + SEQSTAT2 + PEAKSTAT1 + PEAKSTAT2 )

rule all:
    input: TARGET

# ---- all the following rules are common rules for analyzing a single dataset ---

rule atac_map:
    input:
        fasta = config["genome"]["fasta"],
        fastq1 = "%s/{fastqid}_R1.fastq.gz" % (config["fastqdir"]),
        fastq2 = "%s/{fastqid}_R2.fastq.gz" % (config["fastqdir"])
    output:
        bam = temp("Result/minimap2/{fastqid}.sortedByPos.bam")
    threads:
        config["options"]["cores"]
    shell:
        "minimap2 -ax sr -t {threads} {input.fasta} {input.fastq1} {input.fastq2} "
        "| samtools view --threads {threads} -b"
        "| samtools sort --threads {threads} -o {output.bam}"

rule atac_bammkdp:
    input:
        bam = "Result/minimap2/{fastqid}.sortedByPos.bam"
    output:
        bam = "Result/minimap2/{fastqid}.sortedByPos.mkdp.bam",
        metric = "Result/minimap2/{fastqid}.sortedByPos.mkdp.txt",
        tmp = temp(directory("Result/Tmp/{fastqid}"))
    shell:
        "picard MarkDuplicates INPUT={input.bam} OUTPUT={output.bam} METRICS_FILE={output.metric} TMP_DIR={output.tmp};"
        "rm {input.bam}"

rule atac_callpeak:
    input:
        bam = "Result/minimap2/{fastqid}.sortedByPos.mkdp.bam" 
    output:
        peak = "Result/Analysis/{fastqid}_peaks.narrowPeak",
        bdg = "Result/Analysis/{fastqid}_treat_pileup.bdg",
        bw = "Result/Analysis/{fastqid}_treat_pileup.bw",
        bam = "Result/minimap2/{fastqid}.sortedByPos.rmdp.clean.bam",
	xls = "Result/Analysis/{fastqid}_peaks.xls",
    params:
        name = "{fastqid}",
        chrombed = config["genome"]["chromBed"],
	chromlen = config["genome"]["chromInfo"]
    log:
        "Result/Log/{fastqid}_macs2_peak.log"
    benchmark:
        "Result/Benchmark/{fastqid}_callpeak.benchmark"
    shell:
        "samtools view --threads {threads} -b -L {params.chrombed} -F 400 -o {output.bam} {input.bam};"
        "macs2 callpeak -g hs --outdir Result/Analysis -n {params.name} --keep-dup all -B -q 0.05 -f BAMPE --SPMR -t {output.bam};"
        "bdg2bw {output.bdg} {params.chromlen}"

rule atac_qcstat:
    input:
        bam = "Result/minimap2/{fastqid}.sortedByPos.rmdp.clean.bam",
        peak = "Result/Analysis/{fastqid}_peaks.narrowPeak",
    output:
        qc_stat = "Result/QC/{fastqid}.stat.txt",
        bam = "Result/minimap2/{fastqid}.sortedByPos.rmdp.clean.unique.bam",
        bed = "Result/minimap2/{fastqid}.sortedByPos.rmdp.clean.unique.bed",
    params:
        promoter = "%s/%s_promoter.bed" %(config["annodir"], config["options"]["species"]),
    threads:
        config["options"]["cores"]
    benchmark:
        "Result/Benchmark/{fastqid}_BulkQCStat.benchmark"
    shell:
        "echo 'flagstat:' > {output.qc_stat};"
        "samtools flagstat --threads {threads} {input.bam} >> {output.qc_stat};"
        "samtools view -F 2316 -f 0x2 -q 30 -b -o {output.bam} {input.bam};"
        "echo 'mapped Q30 reads:' >> {output.qc_stat};"
        "samtools view {output.bam} -c >> {output.qc_stat};"
        "bedtools bamtobed -i {output.bam} > {output.bed};"
        "echo 'chrM reads:' >> {output.qc_stat};"
        "grep -c 'chrM' {output.bed} >> {output.qc_stat} || true;"
        "echo 'non chrM reads:' >> {output.qc_stat};"
        "grep -v 'chrM' -c {output.bed} >> {output.qc_stat};"
        "echo 'non chrM reads in promoter:' >> {output.qc_stat};"
        "grep -v 'chrM' {output.bed} | bedtools intersect -wa -a - -b {params.promoter} -u | wc -l >> {output.qc_stat} || true;"
        "echo 'non chrM reads in peak:' >> {output.qc_stat};"
        "grep -v 'chrM' {output.bed} | bedtools intersect -wa -a - -b {input.peak} -u | wc -l >> {output.qc_stat} || true ;"

rule atac_peakqc:
    input:
        peak = "Result/Analysis/{fastqid}_peaks.narrowPeak",
        peakxls = "Result/Analysis/{fastqid}_peaks.xls",
    output:
        peak_qc = "Result/QC/{fastqid}.peakstat.txt",
    params:
        promoter = "%s/%s_promoter.bed" %(config["annodir"], config["options"]["species"]),
        chrMregion = "%s/%s_chrM.bed" %(config["annodir"], config["options"]["species"]),
        blacklist = "%s/blacklist.bed" % (config["annodir"]),
        DHS = "%s/DHS.bed" % (config["annodir"]),
    threads:
        config["options"]["cores"]
    benchmark:
        "Result/Benchmark/{fastqid}_PeakQCStat.benchmark"
    shell:
        "grep 'total fragments in treatment' {input.peakxls} | perl -pe 's/# //' > {output.peak_qc};"
        "grep 'fragments after filtering in treatment' {input.peakxls} | perl -pe 's/# //' >> {output.peak_qc};"
        "echo 'total number of peaks:' >> {output.peak_qc};"
        "wc -l {input.peak} | cut -f 1 -d' ' >> {output.peak_qc};"
        "echo 'number of peaks over FC 2:' >> {output.peak_qc};"
        "awk '$7>=2{{print}}' {input.peak} | wc -l >> {output.peak_qc};"
        "echo 'number of peaks in blacklist regions:' >> {output.peak_qc};"
        "bedtools intersect -a {input.peak} -b {params.blacklist} -u | wc -l >> {output.peak_qc};"
        "echo 'number of peaks in chrM:' >> {output.peak_qc};"
        "grep -w chrM {input.peak} | wc -l >> {output.peak_qc};"
        "echo 'number of peaks in promoter regions:' >> {output.peak_qc};"
        "bedtools intersect -a {input.peak} -b {params.promoter} -u | wc -l >> {output.peak_qc};"
        "echo 'number of peaks in DHS regions:' >> {output.peak_qc};"
        "bedtools intersect -a {input.peak} -b {params.DHS} -u | wc -l >> {output.peak_qc};"
